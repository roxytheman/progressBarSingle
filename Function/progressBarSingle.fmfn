// PROGRESS BAR SINGLE

// Generates a horizontal progress bar based on a single percentage 
// value, using two custom colours to indicate completed and remaining progress.

// Function Name : progressBarSingle
// Parameters : width ; height ; radius ; colorBackground ; colorFilled ; pctFilled
// Use example : progressBarSingle ( 400 ; 16 ; 5 ; "#FFFFFF" ; "#CDCDCD" ; 70 )

Let ( [

		// Base Values
		~colorBackground = colorBackground ;
		~colorFilled = colorFilled ;
		~width = Round ( width * ( 100 / Get ( WindowZoomLevel ) ) ; 0 ) ;
		~height = Round ( height * ( 100 / Get ( WindowZoomLevel ) ) ; 0 ) ;
		~radius = radius ; 
		~pctFilled = pctFilled ;
		~scaleFix = 1 ; 

		// Progress ~width
		~filledWidth = Max ( 0 ; Min ( ~width ; Round ( ~width * ~pctFilled / 100 ; 0 ) ) ) ;

		// Define the SVG string
		~svg = 
		
			"<?xml version='1.0' encoding='UTF-8'?>" & 


				If ( Get(Device) > 2 ; 
		
					"<svg xmlns=\"http://www.w3.org/2000/svg\" style=\"width:100%;height:100%; \"viewBox=\"0 0 " & ~width & " " & ~height & "\" preserveAspectRatio=\"xMidYMid meet\">"  ;
					"<svg xmlns='http://www.w3.org/2000/svg' width='100%' height='100%' viewBox='0 0 " & ~width & " " & ~height & "'>" 
		
				) &
			
				// Background bar
				"<rect x='0' y='0' width='" & ~width & "' height='" & ~height & "' rx='" & ~radius & "' ry='" & ~radius & "' fill='" & ~colorBackground & "' />" &
			
				// Filled progress bar
				If ( 
					~filledWidth > 0 ; 
			
					"<clipPath id='clip'>" &
						"<rect x='0' y='0' width='" & ~filledWidth & "' height='" & ~height & "' rx='" & ~radius & "' ry='" & ~radius & "' />" &
					"</clipPath>" &
					"<rect x='0' y='0' width='" & ~filledWidth & "' height='" & ~height & "' fill='" & ~colorFilled & "' clip-path='url(#clip)' />" ;
			
					""
				) &
			
			"</svg>" 

	]; 

		Case ( 
			PatternCount ( Get ( ApplicationVersion ) ; "Web" ) ;
			"data:image/svg+xml;base64," & Base64Encode ( Substitute ( ~svg ; "¶" ; "" ) ) ;

			Get( Device ) > 2 ;

						"data:text/html," & 

				"<html>
				<meta name=\"viewport\" content=\"width=device-width, initial-scale=" & ~scaleFix & ", maximum-scale=" & ~scaleFix & ", user-scalable=no\">
				  <body style=\"margin:0;padding:0;background:#" & ~colorBackground & ";\">"
				     & ~svg &
				  "</body>
				</html>" ;

			"data:image/svg+xml;utf8," & Substitute ( ~svg ; "¶" ; "" )
		)
)

// This function produces a horizontal SVG-based progress bar that visually represents 
// a percentage complete value between 0 and 100. The bar is rendered with two colours: 
// one for the completed portion and another for the remaining segment. Rounded corners are applied, 
// and the bar scales responsively to the size of the layout object if configured accordingly. 

// The output is Base64-encoded and intended for use in web viewers or for conversion into a PNG graphic.

// GitHub : https://github.com/roxytheman/fmCustomFunctions/blob/c84842779eba9a9088a2e5a9beae4079af7e6b7a/functions/progressBarSingle.fmfn
// BD Link : https://www.briandunning.com/cf/2800
// Written by: jonni@me.com
// V 1.2